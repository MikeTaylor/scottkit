start cave
treasury throne

action score: score
action inventory: inventory
action look: look

occur when !flag 1
	print "Welcome to the Tutorial adventure."
	print "You must find a gold coin and store it."
	set_flag 1

room cave "cave mouth"

item doorbell "doorbell"

item entrance "entrance to cave"
	nowhere

action go entrance when here entrance
	goto chamber
	look

action ring doorbell when here doorbell and !here entrance
	set_counter 4
	print "Ding dong!"

occur
	dec_counter

occur when counter_eq 2
	print "I hear an ominous rumble."

occur when counter_eq 1
	put entrance cave
	print "The rocks collapse leaving a way in."
	look

action wait:
	print "Time passes ..."

room throne "gorgeously decorated throne room"
	exit south chamber

item sign "Sign says: leave treasure here, then say SCORE"

item lamp "old-fashioned brass lamp"
	called "lamp"

lightsource lamp

room chamber "square chamber"
	exit east dungeon
	exit north throne
	exit west cave

# Flag 15 is on when and only when it is dark
occur when "at" chamber and flag 15
	clear_dark
	look

item cross "Wooden cross"
	called "cross"

room dungeon "gloomy dungeon"
	exit west chamber
	exit north crypt

occur when "at" dungeon and !flag 15
	set_dark
	look

occur 25% when "at" dungeon
	print "I smell something rotting to the north."

item door "Locked door"

item key "Brass key"
	called "key"
	at crypt

item door2 "Open door leads south"
	nowhere

action open door when here door and !present key
	print "It's locked."

action open door when here door
	swap door door2
	print OK
	look

action go door when here door2
	goto cell
	look

room cell "dungeon cell"
	exit north dungeon

item coin "*Gold coin*"
	called "coin"

room crypt "damp, dismal crypt"
	exit south dungeon

item vampire "Vampire"

occur when here vampire and !carried cross
	print "Vampire bites me!  I'm dead!"
	game_over
	comment "vampire attacks unless cross is carried"

occur when here vampire and "carried" cross
	print "Vampire cowers away from the cross!"

verbgroup take get
verbgroup leave drop
noungroup lamp lantern
noungroup doorbell bell
