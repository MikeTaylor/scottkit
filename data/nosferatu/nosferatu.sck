# NOSFERATU: a vampire-themed adventure game
# By Mike Taylor, copyright (C) 1982, 2001 and 2017

# Use of flags:
# 1: Start-of-game message has been printed
# 2: THROW AXE has been issued: awaiting AT THING
# 3: TIE ROPE has been issued: awaiting TO THING
# 4: Whisky has been drunk

ident 18401
version 1
wordlen 3
maxload 5
start coffin
treasury hidden
lightsource litlamp
lighttime 999
unknown1 19540
unknown2 0

verbgroup WALK ENTER GO RUN
verbgroup LEAVE DROP
verbgroup TAKE GET
verbgroup OPEN UNLOCK
verbgroup LOOK L

noungroup AXE AX

room hidden "hidden treasury"
	item treasure "*treasure* (you shouldn't be able to see this)" nowhere

room coffin "tall, satin-lined wooden box."
    exit east empty

room empty "*I'm in an empty room."
    exit north kitchen
    exit south crypt
    exit east balcony
    exit west coffin

room crypt "dark crypt."
    exit north empty
    exit east library
	item sarcophagus "stone Sarcophagus"
	item body "decomposing Body" nowhere
	item dust "pile of Dust" nowhere
	item cross "wooden Cross" called CROSS nowhere

room library "*I'm in the library."
    exit north balcony
    exit west crypt

	item atlas "transylvanian Atlas" called ATLAS
	item magic "book of Magic" called MAGIC
	item games "book of Games" called GAMES

room balcony "*I'm on a balcony overlooking a tall cliff."
    exit south library
    exit west empty
	item ropetied "rope tied to rail" nowhere
	item rail "balcony Rail"

room ledge "*I'm on a narrow ledge high up on a cliff."
	item key "brass Key" called KEY
	item kipper "scarlet Kipper" called KIPPER
	item hanging "hanging Rope"

room kitchen "*I'm in the kitchen"
    exit north washroom
    exit south empty
	item whisky "bottle of Whisky" called WHISKY
	item emptybottle "empty whisky Bottle" called BOTTLE nowhere

room washroom "*I'm in the washroom."
    exit south kitchen
    exit east mortuary
	item lockeddoor "locked Door"
	item opendoor "wide open Door" nowhere
	item rope "long Rope" called ROPE

room mortuary "*I'm in the mortuary."
    exit north graveyard
    exit west washroom
	item mallet "7-pound Mallet" called MALLET

room graveyard "desolate windswept graveyard."
    exit north busstop
    exit south mortuary
	item grave "newely dug open Grave"
	item tombstone "tombstone engraved: BEWARE THE WITCH"

room busstop "*I'm by a bus-stop."
    exit south graveyard
	item bus "large red no. 29 Bus" nowhere

room cave "dark gloomy cave."
	item witch "wrinkled old Witch"
	item gravehole "Hole (above me)"
	item lamp "old Lamp" called LAMP nowhere
	item litlamp "lit Lamp" called LAMP nowhere

room bus "*I'm on a double-decker bus."
    exit south busstop
	item conductor "bus Conductor"

room yard "rather dark yard."
    exit north shed
    exit east washroom
    exit west pond
	item axe "sharp Axe" called AXE

room shed "daek shed"
    exit south yard
	item ladder "tall Ladder" called LADDER

room pond "*I'm by a fishpond ..."
    exit south swamp
    exit east yard
	item shark "maneating Shark!"

room swamp "damp swamp."
    exit north pond
    exit east grove # This exit does not exist in the original
    exit west junction

room junction "*I'm on a track. To my North is a bridge over a pond."
    exit north bridge
    exit east swamp
    exit west field

room bridge "*I'm on a very flimsy bridge..."
    exit north lawn
    exit south junction
	# XXX should fall to death if carrying more than one item

room lawn "*I'm on a lawn to the North of a point."
    exit north hut # This exit does not exist in the original
    exit south bridge
	item hut "Hut with locked door"
	item smashed "Hut with smashed door" nowhere

room hut "small hut"
    exit south lawn
	item spade "garden Spade" called SPADE
	item stick "sharp wooden Stick" called STICK
		# The stick seems to be a complete red herring

room westpond "*I'm west of a pond."
    exit south field
    exit west cliff

room field "summy field."
    exit north westpond
    exit south forest
    exit east junction
    exit west bythicket
	item crucifix "crusifix engraved on the ground"
	item pit "Pit in the ground" nowhere

room forest "forest"
    exit north field
    exit west inpit

room inpit "*I'm in a pit"

room bythicket "*I'm north of a rather smelly hole."
    exit north cliff
    exit south inpit
    exit east field
	item wall2 "brick Wall"

room cliff "*I'm at the top of a cliff."
    exit south bythicket
    exit east westpond
	item thicket "inpenetrable Thicket"
	item mutilated "mutilated thicket" nowhere
	item sign1 "Sign here says: 'EXA NO FO TROHS'"
	item sign2 "SHORT OF AN AXE' is written backwards on a Sign nearby'" nowhere

room chasm "*I'm on a narrow ledge, East of a chasm."
    exit north edge
	item wall1 "brick Wall"
	item match "safety Match" called MATCH

room edge "*I'm at the edge of a deep chasm."
    exit south chasm
    exit east cliff
	item water "Water of youth"
	item fountain "Fountain of youth"
	item holeinthicket "Hole in thicket"
	item bottle "whisky Bottle" called BOTTLE nowhere

room bottom "*I'm at the bottom of a deep hole."
	item tunnel "secret Tunnel"

room cavern "subterranean cavern."
    exit south bottom
	item stalactite "Stalactites hanging from the ceiling"
	item entrance "entrance to cave" nowhere

room underground "cavern where the count sleeps."
    exit south cavern
	item count "the count Nosferatu"
	item altar "stone Altar"
	item bloodstone "giant *BLOODSTONE*" called BLOODSTONE

room grove "hidden grove"
    exit west swamp
	item coins "coins" called COINS

action INVENTORY:
	inventory

action TAKE INVENTORY
	inventory

action SCORE:
	score

action SAVE GAME
	save_game

occur when !flag 1
	set_flag 1
	print "Welcome to `NOSFERATU`, by Mike Taylor."
	print "  Original VIC-20 BASIC version, 1982"
	print "  Translation to Games::ScottAdams starting Sunday 25th November 2001"
	print "  Translation to ScottKit starting Saturday 14 October 2017"
	print "This is a demo for the `ScottKit` adventure system --"
	print "See https://github.com/MikeTaylor/scottkit/"
	println
	print "Your task is to kill the evil count Nosferatu and get back home."

action DIG when !present spade
	print "I'll need a spade."

occur when at cavern and !flag 15
	set_dark
	look # XXX should only do this when changing state
	comment "everywhere inside the cavern is dark"

occur when at bottom and flag 15
	clear_dark
	look # XXX should only do this when changing state
	comment "everywhere outside the bottom of the pit is light"

occur when at underground and !carried cross
	print "Nosferatu rises from his altar, and bites my neck!"
	game_over

action TIE ROPE when !present rope
	print "Huh?  What rope?"

action TIE ROPE
	print "To what (eg. to key)"
	set_flag 3

action TO RAIL when flag 3 and at balcony
	clear_flag 3
	destroy rope
	drop ropetied
	print "OK."

action TO when flag 3
	clear_flag 3
	print "What a stupid concept."

action TO:
	print "Huh?"

action CLIMB ROPE when here ropetied
	goto ledge

action CLIMB ROPE when at ledge and carried kipper
	print "I'm too heavy: I fall!"
	game_over
	comment "We would rather this happen carrying more than one object, but this is near enough."

action CLIMB ROPE when at ledge
	goto balcony

action OPEN DOOR when present key and here lockeddoor
	swap lockeddoor opendoor
	print "OK."
	look
	comment "In the original, this created a new exit leading west, but we can't do that in the Scott Adams engine. Instead, the player must GO DOOR."

action GO DOOR when here opendoor
	goto yard

action GO GRAVE when here grave
	goto cave

action WAIT when !here bus
	drop bus
	print "The bus arrives."

action GO BUS when here bus
	goto bus

action DROP BOTTLE when here witch and carried water
	destroy water
	drop bottle
	continue occur 0%
	swap witch lamp
	print "The witch drinks the water, rejuvenates, drops something, and disappears!"
	look

action GIVE COINS when here conductor
	print "He takes them and rings a bell.  The bus starts."
	pause
	print "You arrive home."
	game_over

action DIG when at field
	swap crucifix pit
	print "I break through to an underground chamber!"
	look

action GO PIT when here pit
	goto bottom

action CLIMB LADDER when here ladder
	goto forest

action JUMP when at bythicket
	goto chasm

action JUMP when at chasm
	goto bythicket

action GET WATER when present emptybottle and here fountain
	print OK
	swap emptybottle bottle
	superget water

action POUR WATER when carried water
	print OK
	destroy water
	swap bottle emptybottle

action THROW AXE when !present axe
	print "Huh?  What axe?"

action THROW AXE
	print "At what (eg. at bus)"
	set_flag 2

action AT DOOR when flag 2 and here hut
	clear_flag 2
	drop axe
	continue occur 0%
	swap hut smashed
	print "It shatters!"
	look

action GO DOOR when here smashed
	goto hut

action AT STALACTITE when flag 2 and here stalactite
	clear_flag 2
	drop axe
	continue occur 0%
	swap stalactite entrance
	print "It shatters!"
	look

action AT when flag 2
	clear_flag 2
	drop axe
	print "OK."
	print "Nothing happens."

action GO ENTRANCE when here entrance
	goto underground

action KILL COUNT when here count
	print "OK."
	print "I punch him and he crumbles to dust."
	print "Huh!  That was easier than I expected!"
	put treasure hidden

action TELEPORT COFFIN
	print ">Fzing!<"
	goto coffin

action READ BOOK
	print "Which one?"

action READ ATLAS when present atlas
	print "The book says:\n\nNear the forest is a disused cess-pet."

action READ MAGIC when present magic
	print "The book says:\n\nMagic word id: 'OVYEZ'"

action READ GAMES when present games
	print "The book says:\n\nBored with this game already, huh?"

action DRINK WHISKY when present whisky
	print "OK."
	print "Burp!  It's good!"
	print "I feel stronger."
	swap whisky emptybottle
	set_flag 4

action OPEN SARCOPHAGUS when here sarcophagus and flag 4
	print "I find something!"
	drop body
	drop cross
	look

action OPEN SARCOPHAGUS when here sarcophagus
	print "It's too heavy!"

action GET BODY when here body
	print "It turns to dust!"
	destroy body
	drop dust

action GET DUST when here dust
	print "It slips through my fingers"

action CHOP THICKET when carried axe and here thicket
	swap thicket mutilated
	continue occur 0%
	swap sign1 sign2
	print "OK"

action GO THICKET when here mutilated
	goto edge

action GO HOLE when here gravehole
	goto graveyard

action GO TUNNEL when here tunnel
	goto cavern

# XXX Should require the match. Check original for message when it's missing
action LIGHT LAMP when present lamp
	swap lamp litlamp
	print "OK"
	look
	comment "We only really want to look if it was previously dark. Can do this with flags."

action HELP:
	print "You'll have to figure it out for yourself."

action EXAMINE:
	print "It looks like a pretty average"
	println_noun

action WAIT:
	print "Nothing happens."

action JUMP:
	print Wheee!

action DIG:
	print "I find nothing."

action LOOK:
	look

