# NOSFERATU: a vampire-themed adventure game
# By Mike Taylor, copyright (C) 1982, 2001 and 2017

# Use of flags:
# 1: Start-of-game message has been printed
# 2: THROW AXE has been issued: awaiting AT THING
# 3: TIE ROPE has been issued: awaiting TO THING
# 4: Whisky has been drunk
# 5: On the bus-journey home

ident 18401
version 1
wordlen 3
maxload 5
start coffin
treasury hidden
lightsource litlamp
lighttime 999
unknown1 19540
unknown2 0

verbgroup GO ENTER WALK RUN
verbgroup TAKE GET G
verbgroup DROP LEAVE D
verbgroup LOOK L
verbgroup INVENTORY I
verbgroup QUIT Q
verbgroup OPEN UNLOCK
verbgroup CHOP CUT

noungroup AXE AX

room hidden "hidden treasury."
	item treasure "*treasure* (you shouldn't be able to see this)" nowhere

room coffin "tall, satin-lined wooden box."
    exit east empty

room empty "*I'm in an empty room."
    exit north kitchen
    exit south crypt
    exit east balcony
    exit west coffin

room crypt "dark crypt."
    exit north empty
    exit east library
	item sarcophagus "stone Sarcophagus"
	item body "decomposing Body" nowhere
	item dust "pile of Dust" nowhere
	item cross "wooden Cross" called CROSS nowhere

room library "*I'm in the library."
    exit north balcony
    exit west crypt

	item atlas "transylvanian Atlas" called ATLAS
	item magic "book of Magic" called MAGIC
	item games "book of Games" called GAMES

room balcony "*I'm on a balcony overlooking a tall cliff."
    exit south library
    exit west empty
	item ropetied "rope tied to rail" nowhere
	item rail "balcony Rail"

room ledge "*I'm on a narrow ledge high up on a cliff."
	item key "brass Key" called KEY
	item kipper "scarlet Kipper" called KIPPER
	item hanging "hanging Rope"

room kitchen "*I'm in the kitchen."
    exit north washroom
    exit south empty
	item whisky "bottle of Whisky" called WHISKY
	item emptybottle "empty whisky Bottle" called BOTTLE nowhere

room washroom "*I'm in the washroom."
    exit south kitchen
    exit east mortuary
	item lockeddoor "locked Door"
	item opendoor "wide open Door" nowhere
	item rope "long Rope" called ROPE

room mortuary "*I'm in the mortuary."
    exit north graveyard
    exit west washroom
	item mallet "7-pound Mallet" called MALLET

room graveyard "desolate windswept graveyard."
    exit north busstop
    exit south mortuary
	item grave "newely dug open Grave"
	item tombstone "tombstone engraved: BEWARE THE WITCH"

room busstop "*I'm by a bus-stop."
    exit south graveyard
	item bus "large red no. 29 Bus" nowhere

room cave "dark gloomy cave."
	item witch "wrinkled old Witch"
	item gravehole "Hole (above me)"
	item lamp "old Lamp" called LAMP nowhere
	item litlamp "lit Lamp" called LAMP nowhere

room bus "*I'm on a double-decker bus."
    exit south busstop
	item conductor "bus Conductor"

room yard "rather dark yard."
    exit north shed
    exit east washroom
    exit west pond
	item axe "sharp Axe" called AXE

room shed "dark shed."
    exit south yard
	item ladder "tall Ladder" called LADDER

room pond "*I'm by a fishpond ..."
    exit south swamp
    exit east yard
	item shark "maneating Shark!" called SHARK

room swamp "damp swamp."
    exit north pond
    exit west junction

room junction "*I'm on a track. To my North is a bridge over a pond."
    exit north bridge
    exit east swamp
    exit west field

room bridge "*I'm on a very flimsy bridge..."
    exit north lawn
    exit south junction
	# XXX should fall to death if carrying more than one item

room lawn "*I'm on a lawn to the North of a pond."
    exit north hut # This exit does not exist in the original
    exit south bridge
	item hut "hut with locked Door"
	item smashed "hut with smashed Door" nowhere

room hut "small hut."
    exit south lawn
	item spade "garden Spade" called SPADE
	item stick "sharp wooden Stick" called STICK
		# The stick seems to be a complete red herring

room westpond "*I'm west of a pond."
    exit south field
    exit west cliff

room field "sunny field."
    exit north westpond
    exit south forest
    exit east junction
    exit west bythicket
	item crucifix "crusifix engraved on the ground"
	item pit "Pit in the ground" nowhere

room forest "*I'm in an oak-forest."
    exit north field
    exit south forest
    exit west inpit
    exit east forest
	item trees "oak-Trees"
	item treesign "Sign says: 'TREES ARE RESERVED FOR COFFINS'"

room inpit "*I'm at the bottom of a disused cesspit."
	item coins "golden Coins" called COINS

room bythicket "*I'm north of a rather smelly hole."
    exit north cliff
    exit south inpit
    exit east field
	item wall2 "brick Wall"

room cliff "*I'm at the top of a cliff."
    exit south bythicket
    exit east westpond
	item thicket "inpenetrable Thicket"
	item mutilated "mutilated thicket" nowhere
	item sign1 "Sign here says: 'EXA NO FO TROHS'"
	item sign2 "SHORT OF AN AXE' is written backwards on a Sign nearby'" nowhere

room chasm "*I'm on a narrow ledge, East of a chasm."
    exit north edge
	item wall1 "brick Wall"
	item match "safety Match" called MATCH
	item spentmatch "spent Match" called MATCH nowhere

room edge "*I'm at the edge of a deep chasm."
    exit south chasm
    exit east cliff
	item water "Water of youth" called water
	item fountain "Fountain of youth"
	item holeinthicket "Hole in thicket"
	item bottle "whisky Bottle" called BOTTLE nowhere

room bottom "*I'm at the bottom of a deep hole."
	item tunnel "secret Tunnel"

room cavern "subterranean cavern."
    exit south bottom
	item stalactite "Stalactites hanging from the ceiling"
	item entrance "entrance to cave" nowhere

room underground "cavern where the count sleeps."
    exit south cavern
	item count "the count Nosferatu"
	item altar "stone Altar"
	item bloodstone "giant *BLOODSTONE*" called BLOODSTONE

action INVENTORY:
	inventory

action TAKE INVENTORY
	inventory

action SCORE:
	score

action SAVE GAME
	save_game

occur when !flag 1
	set_flag 1
	print "Welcome to `NOSFERATU`, by Mike Taylor."
	print "  Original VIC-20 BASIC version, 1982"
	print "  Translation to Games::ScottAdams starting Sunday 25th November 2001"
	print "  Translation to ScottKit starting Saturday 14 October 2017"
	print "This is a demo for the `ScottKit` adventure system --"
	print "See https://github.com/MikeTaylor/scottkit/"
	println
	print "Your task: kill the evil count Nosferatu and return home with his bloodstone."

action DIG HOLE when !present spade
	print "I'll need a spade."

occur when at cavern and !flag 15
	set_dark
	look # XXX should only do this when changing state
	comment "everywhere inside the cavern is dark"

occur when at bottom and flag 15
	clear_dark
	look # XXX should only do this when changing state
	comment "everywhere outside the bottom of the pit is light"

occur when here count and !carried cross
	print "Nosferatu rises from his altar, and bites my neck!"
	game_over

action TAKE BLOODSTONE when here count
	print "Nosferatu rises from his altar, and bites my neck!"
	game_over

action TIE ROPE when !present rope
	print "I don't have it."

action TIE ROPE
	print "To what (eg. to key)"
	set_flag 3

action TIE:
	print "I can't tie a"
	println_noun

action TO RAIL when carried rope and flag 3 and at balcony
	clear_flag 3
	destroy rope
	continue occur 0%
	drop ropetied
	print "OK."
	look

action TO when carried rope and flag 3
	clear_flag 3
	print "I can't tie a rope to a"
	println_noun

action TO when !carried rope and flag 3
	print "I haven't got a rope."

action GET ROPE when here ropetied
	print "It's tied to a rail!"

action CLIMB ROPE when here ropetied
	goto ledge
	look

action CLIMB ROPE when at ledge and carried kipper
	print "I'm too heavy:\nI fall!"
	game_over
	comment "We would rather this happen carrying more than one object, but this is near enough."

action CLIMB ROPE when at ledge
	goto balcony
	look

action OPEN DOOR when present key and here lockeddoor
	swap lockeddoor opendoor
	print "OK."
	look
	comment "In the original, this created a new exit leading west, but we can't do that in the Scott Adams engine. Instead, the player must GO DOOR."

action OPEN DOOR when here lockeddoor
	print "It's locked!"

action OPEN DOOR when present key and here hut
	print "My key won't fit!"

action OPEN DOOR when here hut
	print "It's locked!"

action GO DOOR when here opendoor
	goto yard
	look

action GO GRAVE when here grave
	goto cave
	look

action WAIT when at busstop and !here bus
	print "Time passes..."
	pause
	print "A bus arrives!"
	drop bus
	look

action GO BUS when here bus
	goto bus
	look

action GO BUS
	print "It's not here."

action DROP BOTTLE when here witch and carried water
	destroy water
	drop bottle
	continue occur 0%
	swap witch lamp
	print "The witch drinks the water, rejuvenates, drops something, and disappears!"
	look

action DROP WATER when carried water and !here fountain
	print "It soaks away."
	destroy water

action GIVE COINS when here conductor and !present coins
	print "I don't have it."

action GIVE COINS when here conductor
	print "He takes them, the bus starts, and I'm home"
	set_flag 5

action GIVE: when here conductor
	print "He doesn't want it."

occur when flag 5 and carried bloodstone
	print "with Nosferatu's Bloodstone."
	print "* Congratulations *"
	print "You win!"
	game_over

occur when flag 5 and !carried bloodstone
	print "without Nosferatu's Bloodstone."
	game_over

action DIG HOLE when present crucifix
	swap crucifix pit
	print "I break through to an underground chamber!"
	look

action DIG HOLE when present pit
	print "I've already done it!"

action GO PIT when here pit
	goto bottom
	look

action CLIMB LADDER when !here ladder
	print "It's not here."

action CLIMB LADDER when at bottom
	goto forest
	drop ladder
	look

action CLIMB LADDER when at inpit
	goto forest
	drop ladder
	look

action CLIMB LADDER
	print "not now."

action CLIMB:
	print "I can't climb a"
	println_noun

action JUMP when at bythicket
	goto chasm
	look

action JUMP when at chasm
	goto bythicket
	look

action GET WATER when present emptybottle and here fountain
	print OK
	swap emptybottle bottle
	superget water

action POUR WATER when carried water
	print OK
	destroy water
	swap bottle emptybottle

action THROW AXE when !carried axe
	print "I don't have it."

action THROW AXE
	print "At what (eg. at bus)"
	set_flag 2

action THROW:
       print "I can't throw a"
       println_noun

action AT DOOR when carried axe and flag 2 and here hut
	clear_flag 2
	drop axe
	continue occur 0%
	swap hut smashed
	print "It shatters!"
	look

action GO DOOR when here smashed
	goto hut
	look

action AT STALACTITE when carried axe and flag 2 and here stalactite
	clear_flag 2
	drop axe
	continue occur 0%
	swap stalactite entrance
	print "It shatters!"
	look

action AT when carried axe and flag 2
	clear_flag 2
	drop axe
	print "OK."
	print "It bounces off."

action AT when !carried axe and flag 2
	print "I need an ax."
	comment "For some reason, the original uses 'ax' here rather then 'axe'."

action GO ENTRANCE when here entrance
	goto underground
	look

action KILL COUNT when here count
	print "OK."
	print "I punch him and he crumbles to dust."
	print "Huh!  That was easier than I expected!"
	put treasure hidden

action TELEPORT COFFIN
	print ">Fzing!<"
	goto coffin
	look

action READ BOOK
	print "Which one?"

action READ LIBRARY when at library
	print "OK, but it'll take a while."
	println
	pause
	print "That was boring!  It was in Transylvanian!"

action READ ATLAS when present atlas
	print "The book says:\n\nNear the forest is a disused cess-pit."

action READ MAGIC when present magic
	print "The book says:\n\nMagic word is: 'OVYEZ'"

action READ GAMES when present games
	print "The book says:\n\nBored with this game already, huh?"

action DRINK WHISKY when present whisky
	print "OK."
	print "Burp!  It's good!"
	print "I feel stronger."
	swap whisky emptybottle
	set_flag 4
	set_counter 7

action DRINK WHISKY when present emptybottle
	print "I've already done it!"

occur when counter_gt 1
	dec_counter

occur when counter_eq 1
	dec_counter
	print "I feel weaker."
	clear_flag 4

action OPEN SARCOPHAGUS when here sarcophagus and exists cross
	print "I've already done it!"

action OPEN SARCOPHAGUS when here sarcophagus and flag 4
	print "I find something!"
	drop body
	drop cross
	look

action OPEN SARCOPHAGUS when here sarcophagus
	print "The lid's too heavy!"

action GET BODY when here body
	print "It turns to dust!"
	destroy body
	drop dust
	look

action GET DUST when here dust
	print "It slips through my fingers"

action CHOP THICKET when carried axe and here thicket
	swap thicket mutilated
	continue occur 0%
	swap sign1 sign2
	print "OK"
	look

action CHOP THICKET when here mutilated
	print "I've already done it!"

action CHOP TREE when here trees
	print "What's the point?"

action GO THICKET when here mutilated
	goto edge
	look

action GO HOLE when here gravehole
	goto graveyard
	look

action GO TUNNEL when here tunnel
	goto cavern
	look

action LIGHT: when present spentmatch
	print "Using a SPENT match?"

action LIGHT: when !present match
	print "First I need a match."

action LIGHT LAMP when !present lamp
	print "I don't have it."

action LIGHT LAMP
	swap lamp litlamp
	swap match spentmatch
	print "OK"
	look
	comment "We only really want to look if it was previously dark. Can do this with flags."

action LIGHT:
	swap match spentmatch
	print "It won't burn."

action SAY OVYEZ when here count
	print "OK."
	println_noun
	println
	print "Nosferatu vanishes in a cloud of smoke!"
	destroy count
	look

action SAY:
	print "OK."
	println_noun
	print "Nothing happens"

action HELP:
	print "You'll have to figure it out for yourself."

action EXAMINE:
	print "It looks like a pretty average"
	println_noun

action WAIT:
	print "Time passes..."
	pause
	print "Nothing happens."

action JUMP:
	print Wheee!

action DIG HOLE:
	print "I find nothing."

action DIG:
	print "I can only dig a hole"

action LOOK:
	look

action QUIT:
	game_over

action SCREAM:
	print "OK."
	print "`Yaaaagh!`"
	print "Nothing happens."

action GET SHARK when here shark
	print "He doesn't seem to be too keen on that idea: He's eating me!"
	game_over

# Debugging
action TELEPORT YARD print "Fzing!" goto yard look
action TELEPORT HUT print "Fzing!" goto hut look
action TELEPORT CHASM print "Fzing!" goto chasm look
