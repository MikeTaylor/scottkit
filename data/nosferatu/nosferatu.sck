# NOSFERATU: a vampire-themed adventure game
# By Mike Taylor, copyright (C) 1982, 2001 and 2017

ident 18401
version 1
wordlen 3
maxload 5
start coffin
treasury hidden
lightsource lamp
lighttime 100
unknown1 19540
unknown2 0

verbgroup WALK ENTER GO RUN
verbgroup LEAVE DROP
verbgroup TAKE GET
verbgroup OPEN UNLOCK

room hidden "treasury"
	item sign "sign says: you shouldn't be here!"
	item treasure "*treasure* (you shouldn't be able to see this)" nowhere

room coffin "tall, satin-lined wooden box."
    exit east empty

room empty "*I'm in an empty room."
    exit north kitchen
    exit south crypt
    exit east balcony
    exit west coffin

room crypt "dark crypt."
    exit north empty
    exit east library
	item sarcophagus "stone Sarcophagus"

room library "*I'm in the library."
    exit north balcony
    exit west crypt

	item atlas "transylvanian Atlas"
	item magic "book of Magic"
	item games "book of Games"

room balcony "*I'm on a balcony overlooking a tall cliff."
    exit south library
    exit west empty
	item rail "balcony Rail"
	item ropetied "rope tied to a railing" nowhere

room ledge "*I'm on a ledge"
	item dangling "dangling end of rope"
	item key "key" called KEY

room kitchen "*I'm in the kitchen"
    exit north washroom
    exit south empty
	item whisky "bottle of Whisky" called WHISKY

room washroom "*I'm in the washroom."
    exit south kitchen
    exit east mortuary
	item bluedoor "locked blue door in west wall"
	item blueopen "open blue door in west wall" nowhere
	item rope "long Rope" called ROPE

room mortuary "*I'm in the mortuary."
    exit north graveyard
    exit west washroom
	item mallet "7-pound Mallet" called MALLET

room graveyard "desolate windswept graveyard."
    exit north busstop
    exit south mortuary
	item grave "newely dug open Grave"
	item tombstone "tombstone engraved: BEWARE THE WITCH"

room busstop "*I'm by a bus-stop."
    exit south graveyard
	item bus "large red no. 29 Bus" nowhere

room cave "witch's cave"
    exit up graveyard
	item witch "old witch"
	item lamp "old-fashioned brass lamp" called LAMP nowhere

room bus "*I'm on a double-decker bus."
    exit south busstop
	item conductor "bus Conductor"

room yard "yard"
    exit north shed
    exit east washroom
    exit west pond

room shed "shed"
    exit south yard
	item bottle "empty bottle" called BOTTLE
	item ladder "ladder" called LADDER

room pond "*I'm east of a pond"
    exit south swamp
    exit east yard

room swamp "swamp"
    exit north pond
    exit east grove
    exit west junction

room junction "junction"
    exit north bridge
    exit east swamp
    exit west field

room bridge "*I'm on a bridge"
    exit north lawn
    exit south junction

room lawn "*I'm on a lawn"
    exit north hut
    exit south bridge

room hut "dingy hut"
    exit south lawn
	item spade "spade" called SPADE

room westpond "*I'm west of a pond"
    exit south field
    exit west thicket

room field "field"
    exit north westpond
    exit south forest
    exit east junction
    exit west bythicket
	item crucifix "crucifix engraved on the ground"
	item hole "hole in the ground" nowhere

room forest "forest"
    exit north field
    exit west pit

room pit "*I'm in a pit"
	item axe "axe" called AXE

room bythicket "*I'm by an impenetrable thicket"
    exit north thicket
    exit south pit
    exit east field

room thicket "thicket"
    exit south bythicket
    exit east westpond

room chasm "*I'm on a narrow ledge by a chasm"
    exit north edge

room edge "*I'm at the edge of a chasm"
    exit south chasm
	item fountain "fountain of life"
	item fullbottle "bottle full of water of life" called BOTTLE nowhere

room entrance "entrance to an underground cave"
    exit east cavern
    exit up field

room cavern "dark cavern"
    exit west entrance
	item stalactite "stalactite"
	item passage "passage leading east" nowhere

room underground "underground chamber"
    exit west cavern
	item count "Count Nosferatu sleeps on an altar"

room grove "hidden grove"
    exit west swamp
	item coins "coins" called COINS

action INVENTORY:
	inventory

action TAKE INVENTORY
	inventory

action SCORE:
	score

action SAVE GAME
	save_game

occur when !flag 1
	set_flag 1
	print "Welcome to `NOSFERATU`, by Mike Taylor."
	print "  Original VIC-20 BASIC version, 1982"
	print "  Translation to Games::ScottAdams starting Sunday 25th November 2001"
	print "  Translation to ScottKit starting Saturday 14 October 2017"
	print "This is a demo for the `ScottKit` adventure system --"
	print "See https://github.com/MikeTaylor/scottkit/"
	println
	print "Your task is to kill the evil count Nosferatu and get back home."

action DIG when !present spade
	print "I can't dig without a spade."

occur when at cavern
	set_dark
	look
	comment "everywhere inside the cavern is dark"

occur when at entrance
	clear_dark
	look
	comment "everywhere outside the entrance is light"

action TIE ROPE when !present rope
	print "Huh?  What rope?"

action TIE ROPE
	print "In 2 words tell me to what...like: TO TREE"
	set_flag 3

action TO BALCONY when flag 3 and at balcony
	clear_flag 3
	destroy rope
	drop ropetied
	print "OK."

action TO when flag 3
	clear_flag 3
	print "What a stupid concept."

action CLIMB ROPE when here ropetied
	goto ledge

action CLIMB ROPE when at ledge
	goto balcony

action OPEN DOOR
	print "Which door? Use OPEN BLUE."

action OPEN BLUE when present key and here bluedoor
	swap bluedoor blueopen
	print "OK."

action GO BLUE when here blueopen
	goto yard

action GO GRAVE when here grave
	goto cave

action WAIT when !here bus
	drop bus
	print "The bus arrives."

action GO BUS when here bus
	goto bus

action GIVE WATER when here witch and present fullbottle
	swap bottle fullbottle
	continue

occur 0%
	swap witch lamp
	print "Witch drinks water, rejuvenates, drops something and disappears!"

action GIVE COINS when here conductor
	print "He takes them and rings a bell.  The bus starts."
	pause
	print "You arrive home."
	game_over

action DIG when at field
	swap crucifix hole
	print "I break through to an underground chamber!"

action GO HOLE when here hole
	goto entrance

action CLIMB LADDER when here ladder
	goto forest

action JUMP when at bythicket
	goto chasm

action JUMP when at chasm
	goto bythicket

action FILL BOTTLE when present bottle and here fountain
	print OK
	swap bottle fullbottle

action POUR WATER when present fullbottle
	print OK
	swap bottle fullbottle

action THROW AXE when !present axe
	print "Huh?  What axe?"

action THROW AXE
	print "In 2 words tell me at what...like: AT TREE"
	set_flag 2

action AT STALACTITE when flag 2 and here stalactite
	clear_flag 2
	drop axe
	continue

occur 0%
	swap stalactite passage
	print "Stalactite smashes, revealing a passage beyond!"

action AT when flag 2
	clear_flag 2
	drop axe
	print "OK."
	print "Nothing happens."

action GO PASSAGE when here passage
	goto underground

action KILL COUNT when here count
	print "OK."
	print "I punch him and he crumbles to dust."
	print "Huh!  That was easier than I expected!"
	put_with treasure sign

action TELEPORT COFFIN
	print ">Fzing!<"
	goto coffin

action HELP:
	print "You'll have to figure it out for yourself."

action EXAMINE:
	print "It looks like a pretty average"
	println_noun

action WAIT:
	print "Nothing happens."

action JUMP:
	print Wheee!

action DIG:
	print "I find nothing."

action READ BOOK
	print "Which one?"

action READ ATLAS when present atlas
	print "The book says:\n\nNear the forest is a disused cess-pet."

action READ MAGIC when present magic
	print "The book says:\n\nMagic word id: 'OVYEZ'"

action READ GAMES when present games
	print "The book says:\n\nBored with this game already, huh?"

