# NOSFERATU: a vampire-themed adventure game
# By Mike Taylor, copyright (C) 1982, 2001 and 2017

ident 18401
version 1
wordlen 3
maxload 5
lighttime 100
unknown1 19540
unknown2 0

verbgroup WALK ENTER GO RUN
verbgroup LEAVE DROP
verbgroup TAKE GET
verbgroup OPEN UNLOCK

room home "treasury"
	treasury home

	item sign "sign says: you shouldn't be here!"
		at home

	item treasure "*treasure* (you shouldn't be able to see this)"
		nowhere

room coffin "coffin"
	exit east rats
	start coffin

room rats "rat-ridden room"
	exit north kitchen
	exit south chamber
	exit east balcony
	exit west coffin

room chamber "chamber"
	exit north rats
	exit east library

	item rope "long rope"
		called ROPE
		at chamber

room library "library"
	exit north balcony
	exit west chamber

room balcony "*I'm on a balcony"
	exit south library
	exit west rats

	item railing "rope tied to a railing"
		nowhere

room ledge "*I'm on a ledge"

	item dangling "dangling end of rope"
		at ledge

	item key "key"
		called KEY
		at ledge

room kitchen "kitchen"
	exit north washroom
	exit south rats

	item reddoor "locked red door in east wall"
		at washroom

	item redopen "open red door in east wall"
		nowhere

	item bluedoor "locked blue door in west wall"
		at washroom

room washroom "washroom"
	exit south kitchen

room mortuary "mortuary"
	exit north graveyard
	exit west washroom

room graveyard "graveyard"
	exit north busstop
	exit south mortuary

	item lamp "old-fashioned brass lamp"
		called LAMP
		nowhere

	item grave "open grave"
		at graveyard

room busstop "*I'm at a bus stop"
	exit south graveyard

	item bus "bus"
		nowhere

room cave "witch's cave"
	exit up graveyard

	item witch "old witch"
		at cave

room bus "*I'm on a bus"
	exit down busstop

	item blueopen "open blue door in west wall"
		nowhere

	item conductor "bus conductor"
		at bus

room yard "yard"
	exit north shed
	exit east washroom
	exit west pond

room shed "shed"
	exit south yard

	item bottle "empty bottle"
		called BOTTLE
		at shed

	item ladder "ladder"
		called LADDER
		at shed

room pond "*I'm east of a pond"
	exit south swamp
	exit east yard

room swamp "swamp"
	exit north pond
	exit east grove
	exit west junction

room junction "junction"
	exit north bridge
	exit east swamp
	exit west field

room bridge "*I'm on a bridge"
	exit north lawn
	exit south junction

room lawn "*I'm on a lawn"
	exit north hut
	exit south bridge

room hut "dingy hut"
	exit south lawn

	item spade "spade"
		called SPADE
		at hut

room westpond "*I'm west of a pond"
	exit south field
	exit west thicket

room field "field"
	exit north westpond
	exit south forest
	exit east junction
	exit west bythicket

	item crucifix "crucifix engraved on the ground"
		at field

	item hole "hole in the ground"
		nowhere

room forest "forest"
	exit north field
	exit west pit

room pit "*I'm in a pit"

	item axe "axe"
		called AXE
		at pit

room bythicket "*I'm by an impenetrable thicket"
	exit north thicket
	exit south pit
	exit east field

room thicket "thicket"
	exit south bythicket
	exit east westpond

room chasm "*I'm on a narrow ledge by a chasm"
	exit north edge

room edge "*I'm at the edge of a chasm"
	exit south chasm

	item fountain "fountain of life"
		at edge

	item fullbottle "bottle full of water of life"
		called BOTTLE
		nowhere

room entrance "entrance to an underground cave"
	exit east cavern
	exit up field

room cavern "dark cavern"
	exit west entrance

	item stalactite "stalactite"
		at cavern

	item passage "passage leading east"
		nowhere

room underground "underground chamber"
	exit west cavern

	item count "Count Nosferatu sleeps on an altar"
		at underground

room grove "hidden grove"
	exit west swamp

	item coins "coins"
		called COINS
		at grove

action INVENTORY:
	inventory

action TAKE INVENTORY
	inventory

action SCORE:
	score

action SAVE GAME
	save_game

occur when !flag 1
	set_flag 1
	print "Welcome to `NOSFERATU`"
	print "Written by Mike Taylor, starting Sunday 25th November 2001"
	continue

occur 0%
	print "This is a demo for the `sac` adventure system --"
	print "See http://www.miketaylor.org.uk/tech/advent/sac/"
	println
	print "Your task is to kill the evil count Nosferatu."

action DIG when !present spade
	print "I can't dig without a spade."

occur when at cavern
	set_dark
	look
	comment "everywhere inside the cavern is dark"

occur when at entrance
	clear_dark
	look
	comment "everywhere outside the entrance is light"

action TIE ROPE when !present rope
	print "Huh?  What rope?"

action TIE ROPE
	print "In 2 words tell me to what...like: TO TREE"
	set_flag 3

action TO BALCONY when flag 3 and at balcony
	clear_flag 3
	destroy rope
	drop railing
	print "OK."

action TO when flag 3
	clear_flag 3
	print "What a stupid concept."

action CLIMB ROPE when here railing
	goto ledge

action CLIMB ROPE when at ledge
	goto balcony

action OPEN DOOR
	print "Which door?  Use OPEN RED or OPEN BLUE."

action OPEN RED when present key and here reddoor
	swap reddoor redopen
	print "OK."

action GO RED when here redopen
	goto mortuary

action OPEN BLUE when present key and here bluedoor
	swap bluedoor blueopen
	print "OK."

action GO BLUE when here blueopen
	goto yard

action GO GRAVE when here grave
	goto cave

action WAIT when !here bus
	drop bus
	print "The bus arrives."

action GO BUS when here bus
	goto bus

action GIVE WATER when here witch and present fullbottle
	swap bottle fullbottle
	continue

occur 0%
	swap witch lamp
	print "Witch drinks water, rejuvenates, drops something and disappears!"

action GIVE COINS when here conductor
	print "He takes them and rings a bell.  The bus starts."
	pause
	print "You arrive home."
	game_over

action DIG when at field
	swap crucifix hole
	print "I break through to an underground chamber!"

action GO HOLE when here hole
	goto entrance

action CLIMB LADDER when here ladder
	goto forest

action JUMP when at bythicket
	goto chasm

action JUMP when at chasm
	goto bythicket

action FILL BOTTLE when present bottle and here fountain
	print OK
	swap bottle fullbottle

action POUR WATER when present fullbottle
	print OK
	swap bottle fullbottle

action THROW AXE when !present axe
	print "Huh?  What axe?"

action THROW AXE
	print "In 2 words tell me at what...like: AT TREE"
	set_flag 2

action AT STALACTITE when flag 2 and here stalactite
	clear_flag 2
	drop axe
	continue

occur 0%
	swap stalactite passage
	print "Stalactite smashes, revealing a passage beyond!"

action AT when flag 2
	clear_flag 2
	drop axe
	print "OK."
	print "Nothing happens."

action GO PASSAGE when here passage
	goto underground

action KILL COUNT when here count
	print "OK."
	print "I punch him and he crumbles to dust."
	print "Huh!  That was easier than I expected!"
	put_with treasure sign

action TELEPORT COFFIN
	print ">Fzing!<"
	goto coffin

action HELP:
	print "You'll have to figure it out for yourself."

action EXAMINE:
	print "It looks like a pretty average"
	println_noun

action WAIT:
	print "Nothing happens."

action JUMP:
	print Wheee!

action DIG:
	print "I find nothing."

